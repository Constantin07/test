#!groovy

import jenkins.model.*
import com.cloudbees.jenkins.plugins.customtools.CustomTool
import com.synopsys.arc.jenkinsci.plugins.customtools.versions.ToolVersionConfig
import hudson.tools.*

def Tools = []
Tools.add(name: 'Terraform', url: 'https://releases.hashicorp.com/terraform/0.11.11/terraform_0.11.11_linux_amd64.zip')
Tools.add(name: 'Packer', url: 'https://releases.hashicorp.com/packer/1.3.5/packer_1.3.5_linux_amd64.zip')

def inst = Jenkins.getInstance()
def desc = inst.getExtensionList(com.cloudbees.jenkins.plugins.customtools.CustomTool.DescriptorImpl.class)[0]
def installs = desc.getInstallations()

Tools.each { t ->
  if (installs.find { it.name == t.name }) {
    println "${t.name} is already installed"
  } else {
    println "Installing ${t.name}"

    List installers = new ArrayList()
    installers.add(new ZipExtractionInstaller(
      '',      // node label to use custom tool on
      t.url,   // url
      ''       // subdir
    ))

    List<ToolProperty> properties = new ArrayList<ToolProperty>()
    properties.add(new InstallSourceProperty(installers))

    def newTool = new CustomTool(
      t.name,                     // Name
      '',                         // Installation directory (home)
      properties,                 // Tool properties
      '',                         // Exported paths
      null,                       // labelSpecifics
      ToolVersionConfig.DEFAULT,  // toolVersion
      null                        // additionalVariables
    )
    installs += newTool
  }
}

desc.setInstallations((com.cloudbees.jenkins.plugins.customtools.CustomTool[])installs)
desc.save()
