#!groovy

@NonCPS
def renderJobConfig(template, binding) {
  def engine = new groovy.text.SimpleTemplateEngine()
  return engine.createTemplate(template).make(binding).toString()
}

// Repos to scan
def repos = [
  [ name: 'test', url: 'git@github.com:Constantin07/test.git', branch: 'master', credentials: 'Git' ]
]

pipeline {

  agent any

  options {
     buildDiscarder(logRotator(numToKeepStr: '15'))
     disableConcurrentBuilds()
     skipDefaultCheckout()
  }

  stages {
    stage('Create jobs') {
      steps {
        script {

          repos.each { repo ->
            dir(repo['name']) {
              checkout([$class: 'GitSCM',
                branches: [[name: repo['branch']]],
                extensions: [
                  [$class: 'CloneOption', depth: 0, noTags: true, reference: '', shallow: true],
                  [$class: 'CleanBeforeCheckout']
                ],
                userRemoteConfigs: [[credentialsId: repo['credentials'], url: repo['url']]]
              ])

              findFiles(glob: 'Jenkins/**/Jenkinsfile').each { file->
                //println "Job ${file.path} found."

                // Create folder structure
                def jobPath = file.path.tokenize('/')

                String jobName = jobPath[1..-2].join('/').toString()
                if (jobName == 'seed') return // Skip seed job

                String jobFolder = ''
                String folder = ''
                if (jobPath.size() >= 4) {
                  // Skip top level directory and Jenkinsfile from path
                  jobPath[1..-3].eachWithIndex { f, i ->
                    if(i == 0) {
                      folder = f
                    } else {
                      folder += '/' + f
                    }
                    if (!Jenkins.instance.getItem(folder)) { // Do not create folder if it exists
                      jobFolder += 'folder("' + folder + '") {}' + '\n'
                    } else {
                      println "Folder ${folder} already exists, skipping ..."
                    }
                  }
                }
                //println jobFolder

                // Configuration options
                String description = ''
                String properties = ''
                String parameters = ''
                String triggers = ''
                String extensions = ''

                // Read job config file
                String jobConfigFile = jobPath[0..-2].join('/') + '/config.yml'
                if (fileExists(jobConfigFile)) {
                  Map jobConfig = readYaml(file: jobConfigFile)

                  // Description
                  if (jobConfig.containsKey('description')) {
                    description = 'description(\'' + jobConfig.description + '\')\n'
                  }

                  // Properties
                  if (jobConfig.containsKey('properties')) {
                    properties += 'properties {'
                    jobConfig.properties.each { property ->
                      if (jobConfig.properties.containsKey('githubProjectUrl')) {
                        properties += '\ngithubProjectUrl("' + jobConfig.properties.githubProjectUrl + '")'
                      }
                    }
                    properties += '\n}'
                  }

                  // Parameters
                  if (jobConfig.containsKey('parameters')) {
                    parameters += 'parameters {'
                    jobConfig.parameters.each { param ->
                      String parameterDefinition
                      if (param.type == 'stringParam') {
                        parameterDefinition = '\n$type("$name", "$defaultValue", "$description")'
                      }
                      if (param.type == 'booleanParam') {
                        parameterDefinition = '\n$type("$name", $defaultValue, "$description")'
                      }
                      parameters += renderJobConfig(parameterDefinition, ['type': param.type, 'name': param.name, 'defaultValue': param.defaultValue, 'description': param.description])
                    }
                    parameters += '\n}'
                  }

                  // Triggers
                  if (jobConfig.containsKey('triggers')) {
                    triggers += 'triggers {'
                    if (jobConfig.triggers.containsKey('cron')) {
                      triggers += '\ncron("' + jobConfig.triggers.cron + '")'
                    }
                    if (jobConfig.triggers.containsKey('scm')) {
                      triggers += '\nscm("' + jobConfig.triggers.scm + '")'
                    }
                    if (jobConfig.triggers.containsKey('upstream')) {
                      triggers += '\nupstream("' + jobConfig.triggers.upstream.projects.join(', ') + '", "' + jobConfig.triggers.upstream.threshold + '")'
                    }
                    triggers += '\n}'
                  }

                  // Git extensions
                  if (jobConfig.containsKey('extensions')) {
                    extensions += 'extensions {'

                    if (jobConfig.extensions.containsKey('cleanBeforeCheckout')) {
                      if (jobConfig.extensions.cleanBeforeCheckout) {
                        extensions += '\ncleanBeforeCheckout()'
                      }
                    }

                    if (jobConfig.extensions.containsKey('cloneOptions')) {
                      extensions += '\ncloneOptions{'
                      if (jobConfig.extensions.cloneOptions.containsKey('shallow')) {
                        if (jobConfig.extensions.cloneOptions.shallow) {
                          extensions += '\nshallow(true)'
                        }
                      }
                      if (jobConfig.extensions.cloneOptions.containsKey('noTags')) {
                        if (jobConfig.extensions.cloneOptions.noTags) {
                          extensions += '\nnoTags(true)'
                        }
                      }
                      extensions += '\n}'
                    }

                    if (jobConfig.extensions.containsKey('disableRemotePoll')) {
                      if (jobConfig.extensions.disableRemotePoll) {
                        extensions += '\ndisableRemotePoll()'
                      }
                    }

                    if (jobConfig.extensions.containsKey('pathRestriction')) {
                      extensions += '\npathRestriction{'
                      if (jobConfig.extensions.pathRestriction.containsKey('includedRegions')) {
                        extensions += '\nincludedRegions("' + jobConfig.extensions.pathRestriction.includedRegions.trim() + '")'
                      } else {
                        extensions += '\nincludedRegions("")'
                      }
                      if (jobConfig.extensions.pathRestriction.containsKey('excludedRegions')) {
                        extensions += '\nexcludedRegions("' + jobConfig.extensions.pathRestriction.excludedRegions.trim() + '")'
                      } else {
                        extensions += '\nexcludedRegions("")'
                      }
                      extensions += '\n}'
                    }

                    extensions += '\n}'
                  }

                }

                // Job template
                def template = '''
                  $folder_path
                  pipelineJob('$job_name') {
                    $description
                    $properties
                    $parameters
                    $triggers
                    definition {
                      cpsScm {
                        scm {
                          git {
                            branch('$branch')
                            remote {
                              credentials('$credentials')
                              url('$url')
                            }
                            $extensions
                          }
                        }
                        lightweight(true)
                        scriptPath('$scriptPath')
                      }
                    }
                  }
                '''

                def binding = [
                  'folder_path': jobFolder,
                  'job_name': jobName,
                  'description': description,
                  'properties': properties,
                  'parameters': parameters,
                  'triggers': triggers,
                  'branch': repo['branch'],
                  'credentials': repo['credentials'],
                  'url': repo['url'],
                  'extensions': extensions,
                  'scriptPath': file.path,
                ]

                def rendered = renderJobConfig(template, binding)
                binding = null // Avoid NotSerializableException
                println rendered
                jobDsl(scriptText: rendered)

              } //findFiles
            } // dir
          } // repos
        } //script
      } //steps
    } //stage
  } //stages

}
